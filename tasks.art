_routines: #[]
_tasks: #[]

_section: $[name :string :literal kind :literal][
    if literal? name -> name: to :string name
    
    decorators: #[
        task:    #[hr: '=', bullet: "ãƒŸ", name: upper name] 
        routine: #[hr: ' ', bullet: "ã€ƒ",  name: name] 
    ]
    
    decorator: decorators\[kind] 
    
    hr: decorator\hr
    bullet: decorator\bullet
    name: strip decorator\name

    display: ~"|bullet| |name| |bullet|" | pad.center.with: '~' (sub terminal\width 2)
    divisor: "" | pad.center.with: hr terminal\width
    
    print ""
    print divisor
    print display
    print divisor
    print ""
]

define :routine [
    init: constructor [name :literal :string action :block]
    act: method [][
        do \action
    ]
]

define :task is :routine [
    init: constructor [name :string :literal action :block requires :block defers :block]
    
    act: method [][
        \callAuxiliary \requires
        do \action
        \callAuxiliary \defers
    ]

    callAuxiliary: method [procedures :block][
        loop procedures [name][
            name: to :string name
            instance: null

            if in? name keys _routines [
                _section name 'routine
                instance: _routines\[name]
            ]
            if in? name keys _tasks [
                _section name 'task
                instance: _tasks\[name]
            ]

            do -> instance\act
        ]
    ]
]

routine: $[name :literal :string action :block][
    _routines\[name]: to :routine [name action]
]

task: $[name :literal :string action :block][
    requires: (attr 'requires) ?? []
    defers: (attr 'defers) ?? []
    ensure -> block? requires
    ensure -> block? defers

    _tasks\[name]: to :task [name action requires defers]
]

directory: $[path :string][
    write.directory path null
]

file: $[filename :string action :block][
    requires: (attr 'requires) ?? @[filename]
    asAttr: (attr 'as) ?? 'f

    ensure -> all? (map requires => exists?)

    let asAttr #[
        file: filename
        requires: requires
    ] 

    do action

]

runTask: $[target :string :literal][
    target: to :string target
    ensure.that: ~{Task "|target|" is defined. Did you mean: "|maximum keys _tasks 'tk [jaro target tk]|"?}
        -> in? target keys _tasks

    exec: $[command :string :literal params :string][
        command: to :string command
        print execute join.with: " " @[command params]
    ]
    alias {|>} 'exec

    tsk: _tasks\[target]

    loop (tsk\requires ++ tsk\defers) 'fn [
        fn: to :string fn
        ensure.that: ~{Task/Routine "|fn|" was defined.} 
            -> or? in? fn keys _tasks in? fn keys _routines  
    ]
    
    _section target 'task
    do -> tsk\act

]